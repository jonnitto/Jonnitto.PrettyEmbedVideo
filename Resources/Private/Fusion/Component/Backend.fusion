prototype(Jonnitto.PrettyEmbedVideo:Component.Backend) < prototype(Neos.Fusion:Component) {
    identifier = ${q(node).property('_identifier')}
    setFirstTrackToDefault = null

    @context.setFirstTrackToDefault = ${this.setFirstTrackToDefault}

    @if.render = ${node.context.inBackend}
    lang = ${Carbon.Backend.translate('lang', 'en', [], null, 'Jonnitto.PrettyEmbedVideo')}

    renderer = afx`
        <input class='jonnitto-prettyembedbackend__checkbox' id={'checkbox-' + props.identifier} type='checkbox' />
        <label class='jonnitto-prettyembedbackend__label' for={'checkbox-' + props.identifier} lang={props.lang}>
            <span class='jonnitto-prettyembedbackend__show'>
                {Carbon.Backend.translate('show', 'Show tracks', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
            </span>
            <span class='jonnitto-prettyembedbackend__hide'>
                {Carbon.Backend.translate('hide', 'Hide tracks', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
            </span>
        </label>
        <table class='jonnitto-prettyembedbackend__table' lang={props.lang}>
            <thead>
                <tr>
                    <th>
                        {Carbon.Backend.translate('properties.src', 'File', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
                    </th>
                    <th>
                        {Carbon.Backend.translate('properties.kind', 'Type', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
                    </th>
                    <th>
                        {Carbon.Backend.translate('properties.label', 'Label', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
                    </th>
                    <th class='jonnitto-prettyembedbackend__center'>
                        {Carbon.Backend.translate('properties.srclang', 'Language', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
                    </th>
                    <th>
                        {Carbon.Backend.translate('default', 'Default', [], 'NodeTypes/Content/Track', 'Jonnitto.PrettyEmbedVideo')}
                    </th>
                </tr>
            </thead>
            <Neos.Neos:ContentCollection tagName='tbody' nodePath='' attributes.data-lang={props.lang} />
        </table>
    `
}
