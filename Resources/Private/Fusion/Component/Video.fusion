prototype(Jonnitto.PrettyEmbedVideo:Component.Video) < prototype(Neos.Fusion:Component) {
    configuration = ${Configuration.setting('Jonnitto.PrettyEmbedVideo')}

    lightbox = ${this.configuration.defaults.lightbox}
    autoplay = ${this.configuration.defaults.autoplay}
    controls = ${this.configuration.defaults.controls}
    loop = ${this.configuration.defaults.loop}
    muted = ${this.configuration.defaults.muted}
    preload = ${this.configuration.defaults.preload}
    playsinline = ${this.configuration.defaults.playsinline}
    crossorigin = ${this.configuration.defaults.crossorigin}

    wrapper = ${Configuration.setting('Jonnitto.PrettyEmbedHelper.wrapper')}

    width = null
    height = null

    id = null

    poster = null
    alternativeText = null
    title = null

    playButton = Jonnitto.PrettyEmbedHelper:Button.Play
    pauseButton = Jonnitto.PrettyEmbedHelper:Button.Pause

    sources = null
    tracks = null
    content = ${(Type.isString(this.sources) ? this.sources : '') + (Type.isString(this.tracks) ? this.tracks : '')}
    ownPreview = null

    // Internal
    type = ${this.lightbox ? 'lightbox' : 'inline'}
    setAutoplay = ${this.lightbox ? null : this.autoplay}

    renderer = afx`
        <Jonnitto.PrettyEmbedHelper:Assets />
        <div class={BEM.modifier('jonnitto-prettyembed', {video: true, type: props.type, autoplay: props.setAutoplay})}>
            <video
                id={props.id}
                data-controls={props.controls}
                aria-label={props.title}
                autoplay={props.setAutoplay}
                loop={props.loop}
                muted={props.muted || props.setAutoplay}
                preload={props.preload}
                playsinline={props.playsinline}
                poster={props.poster}
                crossorigin={props.crossorigin}
                width={props.width}
                height={props.height}
            >
                {props.content}
            </video>
            {props.setAutoplay ? '' : props.playButton}
            {props.controls || props.setAutoplay ? '' : props.pauseButton}
            <Neos.Fusion:Augmenter class='jonnitto-prettyembed__preview' @if.set={props.ownPreview || props.poster}>
                {props.ownPreview}
                <img @if.set={props.poster && !props.ownPreview} alt={props.alternativeText} src={props.poster} loading='lazy' />
            </Neos.Fusion:Augmenter>
        </div>
    `
    renderer.@process.wrapper = Jonnitto.PrettyEmbedHelper:Wrapper {
        wrapper = ${props.wrapper}
    }
}
